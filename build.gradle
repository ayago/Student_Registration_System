buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:6.3.0"
    }
}

configure(subprojects.findAll {it.name != 'domain'}) {
    group = 'com.acy.exam.metadata.srs'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.freefair.lombok'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.8
                }
            }
        }
    }

    jacocoTestReport {
        dependsOn test
        reports {
            csv.required = true
            html.required = false
        }
    }

    check.dependsOn jacocoTestCoverageVerification
}

configure(subprojects.findAll {it -> it.name.endsWith("-domain")}) {
    dependencies {
        implementation group: 'io.projectreactor', name: 'reactor-core', version: '3.4.14'
        implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.0.0'
        testImplementation group: 'io.projectreactor', name: 'reactor-test', version: '3.4.14'
        testImplementation group: 'io.projectreactor.tools', name: 'blockhound-junit-platform', version: '1.0.6.RELEASE'
        testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.8.2'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.2.0'
    }
}